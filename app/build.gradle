apply plugin: 'com.android.application'

//continuous integration with travis-ci and jacoco
apply plugin: 'jacoco'

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

   reports {
       xml.enabled false
       csv.enabled false
    }

    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                     'android/**/*.*',
                      '**/data/models/*']

    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
   ])

    // Always regenerate report
    outputs.upToDateWhen { false }
}

jacoco {
    toolVersion = "0.8.1"
}

// For Unit Test
tasks.withType(Test) {
   jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.android.sagot.mynews"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        // continuous integration with travis-ci and Jacoco
       debug {
           testCoverageEnabled true
       }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Add ButterKnife library for more easily link views to activities/fragments
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Add appcompat for use ToolBar and ViewPager
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // Add design support library for use Navigation Drawer and TabLayout
    implementation 'com.android.support:design:28.0.0'

    // Add Rx java2 library for reactive development
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'

    // Add Gson library for serialized and de-serialized object
    implementation 'com.google.code.gson:gson:2.8.2'

    // Add Retrofit Library ( REST client ) for used to make network requests
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Add Support Library for execute network requests with the Streams
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // Add RecyclerView Library
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    // Add Glide Library for show images
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'

    // Add IcePick Library for bundle management
    api 'frankiesardo:icepick:3.2.0'
    compileOnly 'frankiesardo:icepick-processor:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'

    // Add Card View Library
    implementation 'com.android.support:cardview-v7:28.0.0'
}
